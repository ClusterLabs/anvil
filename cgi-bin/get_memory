#!/usr/bin/perl
# 
# This prints JSON formated data reporting the status of RAM on the system.
# 

use strict;
use warnings;
use Anvil::Tools;
use Data::Dumper;
use JSON;

$| = 1;

my $THIS_FILE           =  ($0 =~ /^.*\/(.*)$/)[0];
my $running_directory   =  ($0 =~ /^(.*?)\/$THIS_FILE$/)[0];
if (($running_directory =~ /^\./) && ($ENV{PWD}))
{
	$running_directory =~ s/^\./$ENV{PWD}/;
}

my $anvil = Anvil::Tools->new();

$anvil->Get->switches;

$anvil->Database->connect;
$anvil->Log->entry({source => $THIS_FILE, line => __LINE__, level => 2, secure => 0, key => "log_0132"});
if (not $anvil->data->{sys}{database}{connections})
{
	# No databases, exit.
	$anvil->Log->entry({source => $THIS_FILE, line => __LINE__, level => 0, 'print' => 1, priority => "err", key => "error_0003"});
	$anvil->nice_exit({exit_code => 1});
}

# Read in any CGI variables, if needed.
$anvil->Get->cgi();

$anvil->Database->get_hosts();
$anvil->Database->get_anvils();

print $anvil->Template->get({file => "shared.html", name => "json_headers", show_name => 0})."\n";

my $target     = $anvil->Get->short_host_name();
my $hash       = {};
my $anvil_uuid = "";
if ($anvil->data->{cgi}{anvil_uuid}{value})
{
	$anvil_uuid = $anvil->data->{cgi}{anvil_uuid}{value};
	$anvil->Log->variables({source => $THIS_FILE, line => __LINE__, level => 2, list => { anvil_uuid => $anvil_uuid }});
}
elsif ($anvil->data->{switches}{'anvil-uuid'})
{
	$anvil_uuid = $anvil->data->{switches}{'anvil-uuid'};
	$anvil->Log->variables({source => $THIS_FILE, line => __LINE__, level => 2, list => { anvil_uuid => $anvil_uuid }});
}
if ((not $anvil_uuid) or (not exists $anvil->data->{anvils}{anvil_uuid}{$anvil_uuid}))
{
	$anvil->data->{anvil_status}{anvil_name} = "!!invalid!anvil_uuid!!";
	$anvil->Log->variables({source => $THIS_FILE, line => __LINE__, level => 2, list => { 'anvil_status::anvil_name' => $anvil->data->{anvil_status}{anvil_name} }});
}
else
{
	my $node1_uuid = $anvil->data->{anvils}{anvil_uuid}{$anvil_uuid}{anvil_node1_host_uuid};
	my $node2_uuid = $anvil->data->{anvils}{anvil_uuid}{$anvil_uuid}{anvil_node2_host_uuid};
	my $dr1_uuid   = $anvil->data->{anvils}{anvil_uuid}{$anvil_uuid}{anvil_dr1_host_uuid};
	$anvil->Log->variables({source => $THIS_FILE, line => __LINE__, level => 2, list => { 
		node1_uuid => $node1_uuid,
		node2_uuid => $node2_uuid, 
		dr1_uuid   => $dr1_uuid, 
	}});
	
	$hash->{total}     = 0;
	$hash->{allocated} = 0;
	$hash->{nodes}     = [];
	
	my @hosts = ($node1_uuid, $node2_uuid);
	if ($dr1_uuid)
	{
		push @hosts, $dr1_uuid;
		$anvil->data->{raw}{newest_record}{$dr1_uuid} = 0;
	}
	
	foreach my $host_uuid (@hosts)
	{
		# Do the query
		my $query = "
SELECT 
    a.host_name, 
    b.scan_hardware_ram_total, 
    b.scan_hardware_memory_free, 
    b.scan_hardware_swap_total, 
    b.scan_hardware_swap_free 
FROM 
    hosts a, scan_hardware b 
WHERE 
    a.host_uuid = b.scan_hardware_host_uuid
AND 
    a.host_uuid = ".$anvil->Database->quote($host_uuid)."
;";
		$anvil->Log->variables({source => $THIS_FILE, line => __LINE__, level => 2, list => { query => $query }});
		my $results = $anvil->Database->query({query => $query, source => $THIS_FILE, line => __LINE__});
		my $count   = @{$results};
		$anvil->Log->variables({source => $THIS_FILE, line => __LINE__, level => 2, list => { 
			results => $results, 
			count   => $count, 
		}});
		foreach my $row (@{$results})
		{
			my $host_name                 = $row->[0];
			my $scan_hardware_ram_total   = $row->[1];
			my $scan_hardware_memory_free = $row->[2]; 
			my $scan_hardware_swap_total  = $row->[3];
			my $scan_hardware_swap_free   = $row->[4];
			$anvil->Log->variables({source => $THIS_FILE, line => __LINE__, level => 2, list => { 
				host_name                 => $host_name, 
				scan_hardware_ram_total   => $anvil->Convert->add_commas({number => $scan_hardware_ram_total})." (".$anvil->Convert->bytes_to_human_readable({'bytes' => $scan_hardware_ram_total}).")", 
				scan_hardware_memory_free => $anvil->Convert->add_commas({number => $scan_hardware_memory_free})." (".$anvil->Convert->bytes_to_human_readable({'bytes' => $scan_hardware_memory_free}).")", 
				scan_hardware_swap_total  => $anvil->Convert->add_commas({number => $scan_hardware_swap_total})." (".$anvil->Convert->bytes_to_human_readable({'bytes' => $scan_hardware_swap_total}).")", 
				scan_hardware_swap_free   => $anvil->Convert->add_commas({number => $scan_hardware_swap_free})." (".$anvil->Convert->bytes_to_human_readable({'bytes' => $scan_hardware_swap_free}).")", 
			}});
			
			
			if (not $hash->{total})
			{
				$hash->{total} = $scan_hardware_ram_total;
				$anvil->Log->variables({source => $THIS_FILE, line => __LINE__, level => 2, list => { 'hash->total' => $hash->{total} }});
			}
			elsif ($scan_hardware_ram_total < $hash->{total})
			{
				$hash->{total} = $scan_hardware_ram_total;
				$anvil->Log->variables({source => $THIS_FILE, line => __LINE__, level => 2, list => { 'hash->total' => $hash->{total} }});
			}
			
			push @{$hash->{nodes}}, {
				host_uuid  => $host_uuid, 
				total      => $scan_hardware_ram_total, 
				free       => $scan_hardware_memory_free,
				swap_total => $scan_hardware_swap_total,
				swap_used  => $scan_hardware_swap_free,
			};
		}
	}
	
	# Now get the servers from the Anvil!
	my $query = "
SELECT 
    a.server_uuid, 
    a.server_name, 
    b.server_definition_xml 
FROM 
    servers a, 
    server_definitions b 
WHERE 
    a.server_uuid = b.server_definition_server_uuid 
AND 
    a.server_anvil_uuid = ".$anvil->Database->quote($anvil_uuid)."
;";
	$anvil->Log->variables({source => $THIS_FILE, line => __LINE__, level => 2, list => { query => $query }});
	my $results = $anvil->Database->query({query => $query, source => $THIS_FILE, line => __LINE__});
	my $count   = @{$results};
	$anvil->Log->variables({source => $THIS_FILE, line => __LINE__, level => 2, list => { 
		results => $results, 
		count   => $count, 
	}});
	foreach my $row (@{$results})
	{
		my $server_uuid           = $row->[0];
		my $server_name           = $row->[1];
		my $server_definition_xml = $row->[2];
		$anvil->Log->variables({source => $THIS_FILE, line => __LINE__, level => 2, list => { 
			's1:server_name'           => $server_name, 
			's2:server_uuid'           => $server_uuid, 
			's3:server_definition_xml' => $server_definition_xml, 
		}});
		
		$anvil->Server->parse_definition({
			server     => $server_name, 
			source     => "from_db",
			definition => $server_definition_xml, 
		});
		
		$hash->{allocated} += $anvil->data->{server}{$target}{$server_name}{from_db}{memory};
		$anvil->Log->variables({source => $THIS_FILE, line => __LINE__, level => 2, list => { 
			'hash->allocated' => $anvil->Convert->add_commas({number => $hash->{allocated}})." (".$anvil->Convert->bytes_to_human_readable({'bytes' => $hash->{allocated}}).")",
		}});
	}
	
}

print JSON->new->utf8->encode($hash)."\n";
