#!/usr/bin/perl

use Device::SerialPort;

my @baud_rates = (9600, 115200);

=head2 find_serial_devices

Find a list of serial devices from ttyUSB.* and
then tests the baud speeds of each. Prints a result
if a successful device/baud rate is found.

=cut
sub find_serial_devices
{
  my $dev_path = "/dev";
  my @devices;
  local(*DIRECTORY);
  opendir(DIRECTORY, $dev_path);
  while(my $file = readdir(DIRECTORY))
  {
    if ($file =~ /ttyUSB.*/)
    {
      my $path = $dev_path . "/$file";
      push @devices, $path;
      #print "Device found: $path\n\n";
    }
  }
  print "Testing Serial Devices...\n\n";
  test_serial_devices({devices => \@devices});
}

=head2 test_serial_devices

Tests a list of device/baud rate combinations,
and prints the results of each successful pair.
One combination per device should be usable.

=head2 Parameters;

=head3 devices

An array of serial device paths.

=cut
sub test_serial_devices
{
  my $parameter = shift;
  my @successful_devices;
  my $devices = defined $parameter->{devices} ? $parameter->{devices} : ();

  foreach my $device (@{$devices})
  {
    foreach my $baud_rate (@baud_rates)
    {
      my $device_test = {device => $device, baud_rate => $baud_rate};
      if (test_serial_device($device_test))
      {
        print "Device Found: $device, Baud Rate: $baud_rate\n\n";
        push @successful_devices, $device_test;
        last;
      }
      sleep(2);
    }
  }
}

=head2 test_serial_device

Tests if a device/baud rate combination has proper output, and can be connected to.

=head2 Parameters;

=head3 device

A serial device path name.

=head3 baud_rate

The baud rate to test with.

=cut
sub test_serial_device
{
  my $parameter = shift;
  my $device = defined $parameter->{device} ? $parameter->{device} : "";
  my $baud_rate = defined $parameter->{baud_rate} ? $parameter->{baud_rate} : "";
  my $port = new Device::SerialPort($device, 1) || return 0;

  $port->baudrate($baud_rate);
  $port->parity("none");
  $port->databits(8);
  $port->stopbits(1);
  $port->write("\r");
  $port->lookclear();
  sleep(2);
  my $output = $port->read(256);
  $port->close();
  #print "Testing Device: $device, Baud Rate: $baud_rate\nOutput: $output\n\n";
  # If the output from has at least 5 printable characters in a row, the device/baud rate should be correct.
  return ($output =~ /(?=.*[ -~])[ -~]{5,}.*$/);
}

find_serial_devices();
